buildscript {
 repositories {
  jcenter()
 }
 dependencies {
  classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  classpath 'net.researchgate:gradle-release:2.2.2'
  classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
 }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'se.bjurr.gitchangelog'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
 mavenLocal()
 mavenCentral()
}

dependencies {
 compile 'com.squareup.retrofit2:retrofit:2.0.0'
 compile 'com.squareup.retrofit2:converter-gson:2.0.0'
 compile 'com.squareup.okhttp:okhttp:2.7.5'
 compile 'com.jayway.jsonpath:json-path:2.1.0'
 compile 'com.google.guava:guava:16.0.1'
 compile 'org.eclipse.jgit:org.eclipse.jgit:3.6.2.201501210735-r'
 compile 'com.github.spullara.mustache.java:compiler:0.8.18'
 testCompile 'junit:junit:4.12'
 testCompile 'org.slf4j:slf4j-simple:1.7.13'
 testCompile 'org.assertj:assertj-core:2.3.0'
 testCompile 'org.mockito:mockito-all:1.8.5'
}

test {
 if (System.getProperty('DEBUG', 'false') == 'true') {
  jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
 }
}

eclipse {
 classpath {
  downloadSources = true
  downloadJavadoc = true
 }
}

sourceSets {
 main.java.srcDirs = ['src/main/java' ]
 test.java.srcDirs = ['src/test/java' ]
}

if (JavaVersion.current().isJava8Compatible()) {
 allprojects {
  tasks.withType(Javadoc) {
   options.addStringOption('Xdoclint:none', '-quiet')
  }
 }
}

shadowJar {
 mergeServiceFiles()
 relocate 'com', 'gitchangeloglibshadow.com'
 relocate 'org', 'gitchangeloglibshadow.org'
 relocate 'retrofit2', 'gitchangeloglibshadow.retrofit2'
 relocate 'net', 'gitchangeloglibshadow.net'
 relocate 'okio', 'gitchangeloglibshadow.okio'
 relocate 'okhttp3', 'gitchangeloglibshadow.okhttp3'
}

artifacts {
 archives shadowJar
}

modifyPom {
 project {
  name 'GIT Changelog'
  description 'Library that generates changelog from a GIT repository.'
  url 'https://github.com/tomasbjerre/git-changelog-lib'
  inceptionYear '2015'
   scm {
   url 'https://github.com/tomasbjerre/git-changelog-lib'
   connection 'scm:https://tomasbjerre@github.com/tomasbjerre/git-changelog-lib.git'
   developerConnection 'scm:git://github.com/tomasbjerre/git-changelog-lib.git'
  }

  licenses {
   license {
    name 'The Apache Software License, Version 2.0'
    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    distribution 'repo'
   }
  }

  developers {
   developer {
    id 'tomasbjerre'
    name 'Tomas Bjerre'
    email 'tomas.bjerre85@gmail.com'
   }
  }
 }
}

extraArchive {
 sources = true
 tests = true
 javadoc = true
}

build.dependsOn shadowJar

afterReleaseBuild.dependsOn install, uploadArchives
